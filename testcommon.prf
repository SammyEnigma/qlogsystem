TEMPLATE = app

QT       += testlib network

!contains(TARGET, ^tst_.*):TARGET = $$join(TARGET,,"tst_")

CONFIG   += console testcase
CONFIG   -= app_bundle

target.path = $$PREFIX/tests
INSTALLS += target

CONFIG += testcase

build_path = $${OUT_PWD}/../../qlogsystem

!no-werror {
  QMAKE_CXXFLAGS += -Werror -Wno-error=comment
}

# ugly hack
windows {
  wine {
    check.commands = wine $$check.commands
  }
  check.commands = "cp $${build_path}/qlogsystem*.dll . &&" $$check.commands
} macx {
  # QMAKE_RPATHDIR is unfortunately not working on macos
  check.commands = "env DYLD_LIBRARY_PATH='$${build_path}'" $$check.commands
} else {
  QMAKE_RPATHDIR += $${OUT_PWD}/../../qlogsystem
}

INCLUDEPATH += $${IN_PWD}/qlogsystem
LIBS += -L$${build_path} -lqlogsystem
