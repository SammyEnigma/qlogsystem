VERSION = $$cat($${PWD}/VERSION)
DEFINES += QLOGSYSTEM_VERSION=\\\"$$VERSION\\\"

INCDIR = $${PREFIX}/include
LIBDIR = $${PREFIX}/lib
BINDIR = $${PREFIX}/bin

# this prevents building under "release" and "debug" directories on windows,
# which creates a consistent linking across platforms
CONFIG -= debug_and_release

QMAKE_CXXFLAGS += -O3
CONFIG += c++17

!no-werror {
  QMAKE_CXXFLAGS += -Werror -Wno-error=comment
}

coverage {
  message("Configuring compilation for coverage test.")
  COVERAGE_FLAGS = --coverage -O0
  QMAKE_CXXFLAGS += $$COVERAGE_FLAGS
  QMAKE_CCFLAGS  += $$COVERAGE_FLAGS
  QMAKE_LFLAGS   += $$COVERAGE_FLAGS
  CONFIG += debug
}
